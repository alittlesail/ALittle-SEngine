namespace ALittle;

protected enum GameAccountStatus
{
    CREATE  = 1;        // 刚刚创建
    LOADING = 2;        // 正在加载
    CACHE   = 3;        // 缓存
    ONLINE  = 4;        // 在线
}

public class GameModule
{
    private GameAccount _account;
    
    public ctor(GameAccount account)
    {
        this._account = account;
        this._account.RegisterModule(this);
    }
    // 获取数据的反射信息
    public fun GetDataReflect() : ALittle.StructInfo { return null; }
    // 释放所有内容
    public fun Release() {}
    // 是否有数据
    public fun HasData() : bool { return false; }
    // 加载数据
    public async fun LoadData(IMsgCommon session) {}
    // 备份数据
    public fun BackupData(IMsgCommon session) {}
    
    // 数据加载完成
    public fun OnDataReady() {}
    // 发送模块数据
    public fun OnSendData() {}
    // 登陆操作
    public fun OnLogin() {}
    // 登出操作
    public fun OnLogout() {}
}

public class GameModuleTemplate<T:struct> : GameModule
{
    private T _data;
    
    public fun GetDataReflect() : ALittle.StructInfo { return reflect<T>; }
    
    public fun OnLogin()
    {
        this._account.SendMsg(this._data);
    }

    public fun HasData() : bool { return true; }
    
    public async fun LoadData(IMsgCommon session)
    {
        if (session == null)
        {
            this._account.LoadOneCompleted(lua.tostring(this).." session == null");
            return;
        }
        
        var rflt = this.GetDataReflect();
        if (rflt == null)
        {
            this._account.LoadOneCompleted(lua.tostring(this)..":GetDataReflect() == null");
            return;
        }
        
        var param = new DataServer.GS2DATA_QLoadStruct();
        param.account_id = this._account.GetId();
        param.hash_code = rflt.hash_code;
        var error, result = DataServer.HandleQLoadStruct(session, param);
        if (error != null)
        {
            this._account.LoadOneCompleted(lua.tostring(this).." DataServer.HandleQLoadStruct() failed:"..error);
            return;
        }
        this._data = cast<T>(result.value);
        this._account.LoadOneCompleted(null);
    }
    
    public fun BackupData(IMsgCommon session)
    {
        if (session == null) return;
        
        var rflt = this.GetDataReflect();
        if (rflt == null)
        {
            Error(lua.tostring(this)..":GetDataReflect() == null");
            return;
        }
        
        var param = new DataServer.GS2DATA_NBackupStruct();
        param.account_id = this._account.GetId();
        param.data = new ALittle.ProtocolAnyStruct();
        param.data.hash_code = rflt.hash_code;
        param.data.value = this._data;
        session.SendMsg(param);
    }
}

public struct GS2C_NAccountInfo
{
    string session_code;
    int account_id;
    int gs_route_id;
}

// Game账号
public class GameAccount
{
    private int _id;
    private int _status;
    private IMsgCommon _client;
    private string _session;
    
    // 正在加载的数量
    private int _loading_count;
    // 失败原因
    private string _loading_failed;
    
    private Map<string, GameModule> _module_map;
    private List<GameModule> _module_list;
    
    // 备份定时器
    private int _backup_timer;
    private int _BACKUP_INTERVAL;
    // cache定时器
    private int _cache_timer;
    private int _CACHE_INTERVAL;
    
    public ctor(int id)
    {
        this._id = id;
        this._loading_count = 0;
        this._loading_failed = null;
        this._status = GameAccountStatus.CREATE;
        this._module_map = new Map<string, GameModule>();
        this._module_list = new List<GameModule>();
        
        this._BACKUP_INTERVAL = 60 * 1000;
        this._CACHE_INTERVAL = 30 * 60 * 1000;
        this._session = lua.tostring(lua.math.random(10000, 99999));
    }
    
    protected fun Release()
    {
        var len = ALittle.List_Len(this._module_list);
        while (len > 0)
        {
            this._module_list[len].Release();
            --len;
        }
        this.StopBackupTimer();
        this.StopCacheTimer();
    }
    
    public fun GetId() : int { return this._id; }
    
    protected fun SetClient(IMsgCommon client) { this._client = client; }
    protected fun GetClient() : IMsgCommon { return this._client; }
    
    protected fun GetStatus() : int { return this._status; }
    protected fun SetStatus(int status) { this._status = status; }
    
    protected fun GetSession() : string { return this._session; }
    
    // 注册模块
    protected fun RegisterModule(GameModule module)
    {
        var rflt = reflect(module);
        if (this._module_map[rflt.__name] != null) return;
        this._module_map[rflt.__name] = module;
        ALittle.List_Push(this._module_list, module);
    }

    // 获取模块
    public fun GetModule<T:GameModule>() : T
    {
        var rflt = reflect<T>;
        return this._module_map[rflt.__name];
    }
    
    // 获取所有模块的反射信息，建表信息
    // 如果出现错误，则返回错误信息
    protected fun GetAllDataReflect() : string, List<ALittle.StructInfo>, Map<int, bool>
    {
        // 用来记录所有的结构体反射信息，包括结构体内部的结构体反射信息
        var map = new Map<int, ALittle.StructInfo>();
        // 用来标记那些反射信息是真正的表结构
        var table_map = new Map<int, bool>();
        for (var _, module in this._module_list)
        {
            var rflt = module.GetDataReflect();
            if (rflt != null)
            {
                // 判断主键名必须是account_id
                var primary = rflt.option_map["primary"];
                if (primary != "account_id") return rflt.ns_name.."."..rflt.rl_name.." don't contain primary named 'account_id'", null, null;

                // 检查account_id是否存在
                var field_index = ALittle.List_IndexOf(rflt.name_list, "account_id");
                if (field_index == null) return rflt.ns_name.."."..rflt.rl_name.." don't contain field named 'account_id'", null, null;
                
                // 判断组建类型值必须是int
                var field_type = rflt.type_list[field_index];
                if (field_type != "int") return rflt.ns_name.."."..rflt.rl_name.." field type of 'account_id' must be 'int'", null, null;

                table_map[rflt.hash_code] = true;
                var error = ALittle.CollectStructReflect(rflt, map);
                if (error != null) return error, null, null; 
            }
        }
        
        var rflt_list = new List<ALittle.StructInfo>();
        var count = 0;
        for (var hash_code, info in map)
        {
            ++ count;
            rflt_list[count] = info;
        }
        return null, rflt_list, table_map;
    }
    
    // 启动加载
    protected fun StartLoading(IMsgCommon session)
    {           
        // 先统计模块数量
        this._loading_count = 0;
        for (var _, module in this._module_list)
        {
            if (module.HasData())
                ++this._loading_count;
        }

        // 发送反射信息
        if (!A_GameAccountManager.IsSendModuleReflect(session))
        {
            var error, rflt_list, table_map = this.GetAllDataReflect();
            if (error != null)
            {
                this._loading_failed = error;
                this.LoadAllCompleted();
                return;
            }
            A_GameAccountManager.SendModuleReflect(session, rflt_list, table_map);   
        }
        
        if (this._loading_count == 0)
        {
            this.LoadAllCompleted();
            return;
        }
        
        // 开始加载        
        for (var _, module in this._module_list)
            module.LoadData(session);
    }
    
    // 加载情况
    protected fun LoadOneCompleted(string error)
    {
        -- this._loading_count;
        if (error != null) this._loading_failed = error;
        
        // 处理加载完毕
        if (this._loading_count > 0) return;
        this.LoadAllCompleted();
    }
    
    private fun LoadAllCompleted()
    {
        // 检查当前account是否还是有效的
        var account = A_GameAccountManager.GetAccountById{GameAccount}(this._id);
        if (account != this) return;
        
        // 如果执行加载失败了，就要做移除处理
        if (this._loading_failed != null)
        {
            ALittle.Log("Loading Failed:"..this._loading_failed..", account_id:"..this._id);
            // 放弃租约
            g_GameLeaseManager.ReleaseLease(this._id);
            // 删除账号
            A_GameAccountManager.DeleteAccount(this);

            // 发送强制登出
            if (this._client != null)
            {
                var msg = new GS2C_NForceLogout();
                msg.reason = this._loading_failed;
                this._client.SendMsg(msg);
            }
            return;
        }

        // 数据加载完成
        this.OnDataReady();
        
        if (this._client != null)
        {
            this.SetStatus(GameAccountStatus.ONLINE);
            this.LoginAction();
        }
        else
        {
            this.SetStatus(GameAccountStatus.CACHE);
            this.StartCacheTimer();
        }
        
        // 启动备份定时
        this.StartBackupTimer();
    }
    
    // 启动备份定时
    protected fun StartBackupTimer()
    {
        ALittle.Log("StartBackupTimer, account_id:"..this._id);
        if (this._backup_timer != null) A_LoopSystem.RemoveTimer(this._backup_timer);
        this._backup_timer = A_LoopSystem.AddTimer(this._BACKUP_INTERVAL, bind(this.Backup, this), 0, this._BACKUP_INTERVAL);
    }
    
    // 停止备份定时
    protected fun StopBackupTimer()
    {
        if (this._backup_timer == null) return;
        
        ALittle.Log("StopBackupTimer, account_id:"..this._id);
        A_LoopSystem.RemoveTimer(this._backup_timer);
        this._backup_timer = null;
    }
    
    // 启动cache超时定时器
    protected fun StartCacheTimer()
    {
        ALittle.Log("StartCacheTimer, account_id:"..this._id);
        if (this._cache_timer != null) A_LoopSystem.RemoveTimer(this._cache_timer);
        this._cache_timer = A_LoopSystem.AddTimer(this._CACHE_INTERVAL, bind(this.CacheTimeout, this));
    }
    
    // 停止cache超时定时器
    protected fun StopCacheTimer()
    {
        if (this._cache_timer == null) return;
        
        ALittle.Log("StopCacheTimer, account_id:"..this._id);
        A_LoopSystem.RemoveTimer(this._cache_timer);
        this._cache_timer = null;
    }
    
    // cache超时
    protected fun CacheTimeout()
    {
        ALittle.Log("CacheTimeout, account_id:"..this._id);
        this._cache_timer = null;
        // 备份
        this.Backup();
        // 放弃租约
        g_GameLeaseManager.ReleaseLease(this._id);
        // 删除账号
        A_GameAccountManager.DeleteAccount(this);
    }
    
    // 备份
    protected fun Backup()
    {
        ALittle.Log("Backup, account_id:"..this._id);
        var lease_info = g_GameLeaseManager.GetLease(this._id);
        if (lease_info == null || lease_info.session == null) return;
        for (var _, module in this._module_list)
            module.BackupData(lease_info.session);
    }
    
    // 下线
    protected fun LogoutAction()
    {
        for (var _, module in this._module_list)
            module.OnLogout();
        this.Backup();
    }
    
    // 数据加载完毕
    protected fun OnDataReady()
    {
        for (var _, module in this._module_list)
            module.OnDataReady();
    }
    
    // 上线
    protected fun LoginAction()
    {
        // 发送基本账号信息
        var param = new GS2C_NAccountInfo();
        param.account_id = this._id;
        param.gs_route_id = __CPPAPI_ServerSchedule.GetRouteId();
        param.session_code = this._session;
        this.SendMsg(param);
    
        // 处理所有模块登陆
        for (var _, module in this._module_list)
            module.OnLogin();

        // 发送所有模块信息
        for (var _, module in this._module_list)
            module.OnSendData();
        
        // 发送数据加载完成
        this.SendMsg(new GS2C_NDataReady());
    }
    
    // 发送数据包
    public fun SendMsg<T:struct>(T msg)
    {
        if (this._client == null) return;
        this._client.SendMsg(msg);
    }
}

private var __enable_cmd = false;

[Cmd]
public static EnableCmd(bool enable)
{
    __enable_cmd = enable;
}

public struct CL2GS_QCmd
{
    string cmd;
}

public struct GS2CL_ACmd
{
    string result;
}

[Msg]
public await static HandleCmd(IMsgCommon client, CL2GS_QCmd msg) : GS2CL_ACmd
{
    assert(__enable_cmd, "未开启指令模式");

    var account = A_GameAccountManager.GetAccountByClient{GameAccount}(client);
    assert(account, "账号未登录");

    var cmd = "";
    var cmd_split_index = ALittle.String_Find(msg.cmd, " ");
    if (cmd_split_index == null)
    	cmd = msg.cmd.." "..account.GetId();
    else
    	cmd = ALittle.String_Sub(msg.cmd, 1, cmd_split_index)..account.GetId().." "..ALittle.String_Sub(msg.cmd, cmd_split_index + 1);

    var error, result = tcall(ALittle.ExecuteCommand, cmd);
    assert(error == null, error);

    var rsp = new GS2CL_ACmd();
    rsp.result = result;
    return rsp;
}